      SUBROUTINE IVELT(VTP,AVEVEL,ST,TSI,NSAMPS)
C      IVELT IS A SUBPROGRAM TO CONVERT VELOCITY TIME PAIRS INTO AN ARRAY OF
C  VELOCITIES, EACH SEPARATED IN TIME UNIFORMLY.  (I.E.  THERE'S A VELOCITY
C  FOR EACH TIME SAMPLE.)  LINEAR INTERPOLATION IS USED TO OBTAIN VELOCITIES
C  BETWEEN THE TIMES SPECIFIED.  THE VELOCITY IS HELD CONSTANT FOR TIMES OUTSIDE
C  THE FIRST AND LAST SPECIFIED TIMES. AT LEAST 2 PAIRS MUST BE GIVEN.
C  EXCEED THE BOUNDS OF THE VTP.
C       ARGUMENTS:
C            VTP    - AN ARRAY OF VELOCITY-TIME PAIRS.  VTP(1) IS A VELOCITY,
C                     VTP(2) IS THE TIME, IN SECONDS, OF THE VELOCITY VTP(1),
C                     VTP(3) IS THE NEXT VELOCITY, AND VTP(4) IS THE TIME OF
C                     VTP(3), ETC..  ALL TIMES MUST BE STRICTLY MONOTONICALLY
C                     INCREASING.  IF ST<VTP(2) THEN ALL TIMES FROM ST TO VTP(2)
C                     WILL BE SET TO VTP(1)  ( EXTRAPOLATED.).  LIKEWISE, ALL
C                     TIMES EXCEEDING THE LAST VTP TIME WILL BE SET TO THE LAST
C                     VELOCITY.  THE VTP ARRAY MUST BE TERMINATED BY A TIME <=0
C                     OR A TIME EXCEEDING THE LAST VTP.
C            AVEVEL - AN ARRAY TO RECEIVE THE UNIFORMLY SPACED VELOCITIES.  MUST
C                     BE AT LEAST NSAMPS LONG.
C            ST     - START TIME IN SECONDS OF THE AVEVEL ARRAY.  AVEVEL(1)
C                     WILL HAVE A TIME OF ST ASSOCIATED WITH IT.
C            TSI    - TIME SAMPLE INTERVAL, IN SECONDS.  THE TIME DIFFERENCE
C                     BETWEEN CONSECUTIVE TIME SAMPLES.
C            NSAMPS - THE NUMBER OF TIME SAMPLES.  THE NUMBER OF VELOCITIES TO
C                     COMPUTE AND STORE INTO AVEVEL.
C
C      PAUL HENKART, SCRIPPS INSTITUTION OF OCEANOGRAPHY, MAY 1979
C
      DIMENSION VTP(111),AVEVEL(111)
      TIME=ST
      INDX=1
      N2=1                                                               /* THE POINTER TO A VTP PAIR
C
C    EXTRAPOLATE ANY TIMES LESS THAN VTP(2)
C
   10 IF(TIME.GT.VTP(2)) GO TO 100
C
      AVEVEL(INDX)=VTP(1)
      INDX=INDX+1
      TIME=TIME+TSI
      IF(INDX.GT.NSAMPS) RETURN
      GO TO 10
  100 IF(ST.LE.VTP(N2+3)) GO TO 200
      N2=N2+2                                                            /* STILL TRYING TO FIND A VTP
      IF(VTP(N2+1).GE.0) GO TO 100
      PRINT 120,ST
  120 FORMAT(' ***  ERROR  ***  NO TIME IN THE VTP EXCEEDS THE START ',
     *     'TIME OF ',F10.2,' (IVELT)')
      STOP
C
C    INTERPOLATE VALUES BETWEEN VTP TIMES
C
  200 CONTINUE
      IF(VTP(N2+3).NE.VTP(N2+1)) GO TO 205                              /* CONSTANT VELOCITY
      VT=0.                                                             /* WATCH OUT FOR DIVIDE BY 0.!
      GO TO 210
  205 VT=(VTP(N2+2)-VTP(N2))/(VTP(N2+3)-VTP(N2+1))
  210 CONTINUE
      IF(TIME.LE.VTP(N2+3)) GO TO 300
      N2=N2+2                                                           /*  SET UP THE NEXT VTP PAIR
      IF(VTP(N2+3).GT.0) GO TO 200
      V=AVEVEL(INDX-1)                                                   /*  EXTRAPOLATE TO TO END
      DO 220 I=INDX,NSAMPS
  220 AVEVEL(I)=V
      RETURN
  300 AVEVEL(INDX)=(TIME-VTP(N2+1))*VT+VTP(N2)
      INDX=INDX+1
      TIME=TIME+TSI
      IF(INDX.LE.NSAMPS) GO TO 210
      RETURN
      END
