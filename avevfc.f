      SUBROUTINE AVEVFC(LEVEL,NEXTAD,ITRACE,NSAMPS,INDX1,NPTS1,
     *   INDX2,NPTS2,INDX3,NPTS3,INDX4,NPTS4)
C     AVEVFC PERFORMS AVERAGE AMPLITUDE NORMALIZATION IN THE AP USING THE VECTOR
C  FUNCTION CHAINER.  AVERAGE AMPLITUDE NORMALIZE ADJUSTS THE AVERAGE ABSOLUTE
C  AMPLITUDE WITHIN EACH USER DEFINED WINDOW TO A USER SPECIFIED LEVEL.  IT DOES
C  THIS BY DIVIDING THE SPECIFIED LEVEL BY THE AVERAGE ABSOLUTE VALUE OF THE
C  WINDOW AND THEN APPLYING THIS AS A MULTIPLIER TO THE CENTER OF THE WINDOW.
C  MULTIPLIERS BETWEEN WINDOW CENTERS ARE DETERMINED BY LINEAR INTERPOLATION.
C  MULTIPLIERS BEFORE THE FIRST CENTER ARE THE SAME AS THE FIRST AND
C  ALL THE MULTIPLIERS AFTER THE CENTER OF THE LAST WINDOW ARE THE SAME AS THE
C  MULTIPLIER OF THE CENTER OF THE LAST WINDOW.
C      THE PROGRAM CREATES A SCRATCH TRACE WITH MULTIPLIERS IN IT AND
C  THEN MULTIPLIES IT AGAINST THE DATA TRACE.
C    A MAXIMUM OF 4 WINDOWS MAY BE USED.
C    NOTE THAT THE DISTANCE BETWEEN WINDOW CENTERS MUST BE IN THE AP IMMEDIATELY
C  FOLLOWING THE 4 LEVELS (AT AP ADDRESS LEVELS).  THESE DISTANCES ARE THE
C  FLOATING POINT (REAL) NUMBER OF DATA POINTS.  THE FIRST DISTANCE IS THE
C  DISTANCE BETWEEN THE CENTER OF THE FIRST WINDOW AND THE CENTER OF THE
C  SECOND WINDOW.
C
C  ARGUMENTS:
C  LEVEL  - THE AP ADDRESS OF THE 4 WINDOW LEVELS. THE DISTANCE (IN DATA POINTS)
C           BETWEEN THE WINDOW CENTERS MUST BE AT LEVELS+4,LEVELS+5,.....
C           PUT THE LEVELS IN THE AP PRIOR TO CALLING AVEVFC.
C  NEXTAD- THE AP ADDRESS OF A SCRATCH ARRAY.  MUST BE NSAMPS+10 LONG.
C  ITRACE - THE AP ADDRESS OF THE FIRST DATA WORD OF THE TRACE TO BE NORMALIZED.
C  NSAMPS - THE NUMBER OF SAMPLES IN THE ENTIRE TRACE.  (THE TRACE LENGTH).
C  INDX1  - THE AP ADDRESS OF THE BEGINNING OF THE FIRST NORMALIZE WINDOW.
C  NPTS1  - THE NUMBER OF DATA POINTS IN THE FIRST WINDOW.
C  INDX2  - THE AP ADDRESS OF THE BEGINNING OF THE SECOND NORMALIZE WINDOW.
C  NPTS2  - THE NUMBER OF DATA POINTS IN THE SECOND WINDOW.
C           THE LIST OF WINDOWS MUST TERMINATE WITH THE NUMBER OF POINTS=0
C           (UNLESS ALL 4 WINDOWS ARE GIVEN.)
C  ETC.
C
C  COPYRIGHTED BY:
C  PAUL HENKART, SCRIPPS INSTITUTION OF OCEANOGRAPHY, MAY 1980
C
      LEVELS=LEVEL
      NEXT=NEXTAD
      NEXT1=NEXTAD+1
      INCRE=NEXT1+1
      MULTS=INCRE+1
      NPTS=LEVELS+3
      ICOUNT=0
100   ICOUNT=ICOUNT+1                                                   ! /* THE WINDOW COUNTER
      IF(ICOUNT.EQ.1) GO TO 1
      IF(ICOUNT.EQ.2) GO TO 2
      IF(ICOUNT.EQ.3) GO TO 3
      IF(ICOUNT.EQ.4) GO TO 4
      GO TO 500
1     I=INDX1
      N=NPTS1
      GO TO 200
2     I=INDX2
      N=NPTS2
      GO TO 200
3     I=INDX3
      N=NPTS3
      GO TO 200
4     I=INDX4
      N=NPTS4
      GO TO 200
C
C
200   IF(N.EQ.0) GO TO 500                                              ! /* ANY MORE WINDOWS
      CALL MEAMGV(I,1,NEXT1,N)                                          ! /* FIND THE MEAN ABSOLUTE VALE OF THE WINDOW
      N=N/2                                                             ! /* FIND THE CENTER OF THE WINDOW
      CALL VDIV(NEXT1,0,LEVELS,0,NEXT1,0,1)                             ! /* CONVERT TO A MULTIPLIER
      I=I+N                                                             ! /* THE AP ADDRESS OF THE CENTER OF THE WINDOW
      IF(ICOUNT.EQ.1) GO TO 400                                         ! /*  IS IT THE FIRST WINDOW
      CALL VSUB(NEXT,0,NEXT1,0,INCRE,0,1)                               ! /* SUBTRACT THE LAST MULTIPLIER
      CALL VDIV(NPTS,0,INCRE,0,INCRE,0,1)                               ! /* THE MULTIPLIER INCREMENT BETWEEN WINDOWS
      N=I-LASTI
      NULTS=MULTS-1
      M=N+1
      CALL VRAMP(NULTS,INCRE,NULTS,1,M)
      MULTS=MULTS+N
      LASTI=I
300   ITEMP=NEXT                                                        ! /* CHANGE NEXT AND NEXT1
      NEXT=NEXT1
      NEXT1=ITEMP
      LEVELS=LEVELS+1                                                   ! /*  POINT TO THE NEXT WINDOW LEVEL
      NPTS=NPTS+1
      GO TO 100                                                         ! /* GO DO THE NEXT WINDOW
C
C    DO THE FIRST WINDOW - THE BEGINNING TOF THE CENTER OF THE FIRST WINDOW
C     GET THE SAME MULTIPLIER
C
400   N=I-ITRACE
      LASTI=ITRACE+N
      N=N+1
      CALL VFILL(NEXT1,MULTS,1,N)                                       ! /* FILL FROM THE BEGINNING OF THE TRACE
      MULTS=MULTS+N
      GO TO 300
C
C     DO THE LAST WINDOW - FROM THE CENTER OF THE LAST WINDOW TO THE END
C      OF THE TRACE GET THE SAME MULTIPLIER
C
500   N=NSAMPS-LASTI                                                    ! /* DO THE LAST WINDOW
      N=N-ITRACE                                                        ! /* USE THE LAST MULTIPLIER UNTIL THE END OF THE TRACE
      N=N+1
      NULTS=MULTS-1
      CALL VFILL(NULTS,MULTS,1,N)
C
C       MULTIPLY THE MULTIPLIER TRACE AGAINST THE DATA TRACE
C
      MULTS=INCRE+1
      CALL VMUL(ITRACE,1,MULTS,1,ITRACE,1,NSAMPS)
      RETURN
      END
