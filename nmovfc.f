      SUBROUTINE NMOVFC(IT2,ISCR,IV,IV2,IANSW,NSAMPS)
C    NMOVFC IS AN AP VECTOR FUNCTION CHAINER ROUTINE TO PERFORM MOST OF THE
C  NMO COMPUTATIONS.  NMOVFC ASSUMES THE VELOCITY FUNCTION IS ALREADY IN THE AP
C  AT ADDRESS IV.  NMOVFC ALSO DEMANDS THE START TIME OF THE DATA, THE RANGE,
C AND THE SAMPLE INTERVAL TO BE IN THE AP AT ADDRESS ISCR.  NMOVFC RETURNS
C  IN AP LOCATION IANSW, AN ARRAY OF 16 BIT (PACKED) INDEXES TO BE USED
C  AS INDEXES TO THE UNMOVEDOUT TRACE.
C  NMOVFC WILL PERFOM THE FOLLWING CALCULATIONS (IN ORDER):
C    1)  GENERATE A T**2 VECTOR.
C    2)  SQUARE THE VELOCITY FUNCTION
C    3)  DIVIDE THE RANGE (SQUARED) BY THE SQUARED VELOCITY FUNCTION
C    4)  ADD X**2/V**2 TO THE SQUARED TIME VECTOR
C    5)  TAKE THE SQUARE ROOT OF THE WHOLE THING
C    T=SQRT(T**2+X**2/V**2)
C    6)  SUBTRACT THE FIRST T0 SO THAT THE INDEXES START WITH 1 AT T0
C    7)  ADD HALF THE SAMPLE INTERVAL IN ORDER TO DO ROUNDOFF.
C    8)  DIVIDE BY THE SAMPLE RATE TO CONVERT TO BLOCKS
C    9)  CONVERT THE BLOCKS TO INTEGER AND PACK 2 TO A WORD.
C
C   ARGUMENTS:
C   IT2    - AN AP ADDRESS OF A SCRATCH ARRAY WHERE NMOVFC CAN GENERATE A T
C            (AND T**2) VECTOR.  THIS AP ARRAY MUST BE NSAMPS LONG.
C   ISCR   - AN AP ADDRESS WHERE THE FOLLOWING INFO IS LOCATED
C            ISCR  THE START TIME (IN SECONDS) OF THE DATA
C            ISCR+1 THE RANGE SQUARED
C            ISCR+2 THE SAMPLE INTERVAL (IN SECONDS)
C            ISCR+3 HALF THE SAMPLE INTERVAL (IN SECONDS)
C   IV     - THE AP ADDRESS OF THE VELOCITY FUNCTION.  THE VELOCITY FUNCTION
C            CLOBBERED BY NMOVFC.  THE VELOCITY FUNCTION HAS A VELOVITY AT EVERY
C            MUST HAVE A VELOCITY AT EVERY POINT, CORRESPONDING TO THE TIMES IN
C            IN THE T ARRAY.  IV MUST CONTAIN NSAMPS VELOCITIES.
C   IV2    - THE AP ADDRESS OF A SCRATCH ARRAY THAT NMOVFC CAN USE TO HOLD THE
C            SQUARE OF THE IV ARRAY.  IV2 MUST BE AT LEAST NSAMPS LONG.
C   IANSW  - THE AP ADDRESS OF WHERE NMOVFC SHOULD LEAVE THE ANSWERS. IANSW MAY
C            BE THE SAME AS IT2 OR IV2 (IV NOT RECOMMENDED.)
C   NSAMPS - THE NUMBER OF SAMPLES TO COMPUTE THE NMO FOR.  ALL AP ARRAYS MUST
C            BE NSAMPS LONG.
C
C  COPYRIGHTED BY:
C   PAUL HENKART, SCRIPPS INSTITUTION OF OCEANOGRAPHY, APRIL 1980
C  ALL RIGHTS RESERVED BY THE AUTHOR.
C
      ITEMP=ISCR+2                                                      /* POINT TO THE SAMPLE INTERVAL
      CALL VRAMP(ISCR,ITEMP,IT2,1,NSAMPS)                                /*  BUILD THE T VECTOR
      CALL VMUL(IT2,1,IT2,1,IT2,1,NSAMPS)                                /* SQUARE THE T ARRAY
      CALL VMUL(IV,1,IV,1,IV2,1,NSAMPS)                                  /* SQUARE THE VELOCITY FUNCTION
      JTEMP=ISCR+1                                                       /*  POINT TO THE RANGE
      CALL VDIV(IV2,1,JTEMP,0,IV2,1,NSAMPS)                              /* DIVIDE X**2 BY V**2
      CALL VADD(IV2,1,IT2,1,IV2,1,NSAMPS)                                /* ADD IT TO T**2
      CALL VSQRT(IV2,1,IANSW,1,NSAMPS)                                  /* TAKE THE SQUARE ROOT OF THE WHOLE MESS
      CALL VNEG(ISCR,1,IV2,1,1)                                          /* TAKE THE NEGATIVE OF THE FIRST T0
      CALL VSADD(IANSW,1,IV2,IANSW,1,NSAMPS)                            /* SUBTRACT THE FIRST T0 FROM ALL
      KTEMP=ISCR+3                                                       /* POINT TO HALF THE SAMPLE INTERVAL
      CALL VSADD(IANSW,1,KTEMP,IANSW,1,NSAMPS)                           /* ADD HALF THE SAMPLE INTERVAL
      CALL VDIV(ITEMP,0,IANSW,1,IANSW,1,NSAMPS)                          /* DIVIDE BY THE SAMPLE INTERVAL
      CALL VFIX(IANSW,1,IANSW,1,NSAMPS)                                  /* CONVERT TO 32 BIT INTEGERS
      RETURN
      END
