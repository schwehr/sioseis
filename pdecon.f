      SUBROUTINE PDECON(I1,I2,I3,I4,I5,I6,I7,I8,I9,I10,I11,I12)
C  PDECON DESIGNS A LEAST SQUARES PREDICTION ERROR FILTER
C  AND APPLIES THE FILTER TO THE ENTIRE TRACE.
C
C  THE COMPUTATIONAL STEPS ARE:
C  1)  COMPUTE THE POSITIVE LAGS OF THE AUTOCORRELATION OF THE DESIGN WINDOW.
C  2)  COMPUTE AND ADD TO THE ZERO LAG THE PERCENTAGE PREWHITENING.
C  3)  COMPUTE THE CROSSCORRELATION BETWEEN THE DESIGN WINDOW AND THE DESIRED
C      OUTPUT.
C  4)  SOLVE THE NORMAL EQUATIONS USING THE WIENER-LEVINSON ALGORITHIM.
C  5)  NEGATE THE FILTER COEFS. AND MOVE THE FILTER DOWN THE ARRAY THE
C      PREDICTION DISTANCE.
C  6)  PUT A ONE IN THE FIRST FILTER POINT
C  7)  ZERO THE PREDICTION DISTANCE -1 POINTS AFTER THE FIRST POINT.
C  8)  FILTER THE ENTIRE TRACE.
C
C  ARGUMENTS:
C  I1 = DESIGN - THE AP LOCATION OF THE BEGINNING OF THE DESIGN WINDOW.
C  I2 = NDPTS  - THE NUMBER OF DATA POINTS IN THE DESIGN WINDOW
C  I3 = TRACE  - THE AP LOCATION OF THE BEGINNING OF THE APPLICATION WINDOW.
C  I4 = NPTS   - THE NUMBER OF DATA POINTS TO FILTER (THE LENGTH OF THE
C                APPLICATION WINDOW).
C  I5 = WHITEN - THE AP LOCATION OF A 3 WORD ARRAY CONTAINING:
C            1) THE FRACTION OF THE ZERO LAG TO USE AS PREWHITING (PERCENT/100.)
C            2) THE NUMBER ONE (1)
C            3) A SCRATCH LOCATION FOR PDECON TO PUT A -1 INTO!
C  I6 = NFPTS  - THE NUMBER OF FILTER POINTS IN THE FILTER.  THE AUTOCORRELATION
C                AND THE CROSSCORRELATION AND THE FILTER WILL BE NFPTS LONG.
C  I7 = AUTO   - THE AP LOCATION OF WHERE PDECON CAN PUT THE AUTOCORRELATION AND
C                THE CROSSCORELATION.  MUST BE 2*NFPTS LONG.
C  I8 = PREDIC - THE PREDTICTION DISTANCE.
C  I9 = FILT   - THE AP LOCATION OF WHERE PDECON CAN PUT THE FILTER POINTS.
C                (MUST BE NFPTS+PREDICTION DISTANCE LONG).
C  I10= ERROPR - THE AP LOCATION OF WHERE PDECON CAN PUT THE ERROR OPERATOR.
C                (MUST BE NPTS LONG).
C  I11= RESULT - THE AP LOCATION OF WHERE PDECON CAN LEAVE THE RESULTS.
C                (MUST BE NPTS LONG).
C  I12= SCRATC - THE AP LOCATION OF A SCRATCH ARRAY AT LEAST NPTS+NFPTS LONG.
C
C  COPYRIGHTED BY:
C  PAUL HENKART, SCRIPPS INSTITUTION OF OCEANOGRAPHY, JUNE 1980
C
      IMPLICIT INTEGER*4 (A-Z)
      DESIGN=I1
      NDPTS=I2
      TRACE=I3
      NPTS=I4
      WHITEN=I5
      ONE=WHITEN+1                                                      ! /* THE AP LOCATION OF THE NUMBER 1.
      NFPTS=I6
      AUTO=I7
      PREDIC=I8
      FILT=I9
      ERROPR=I10
      RESULT=I11
      SCRATC=I12
C
      N=NFPTS+PREDIC
      NN=N+1
      CALL CONV(DESIGN,1,DESIGN,1,AUTO,1,NN,NDPTS)                      ! /* COMPUTE THE AUTOCOR
      CALL VDIV(AUTO,1,ONE,1,SCRATC,1,1)
      CALL VSMUL(AUTO,1,SCRATC,AUTO,1,NN)
      CALL VSMA(AUTO,1,WHITEN,AUTO,1,AUTO,1,1)                          ! /* ADD THE PREWHITENER IN
      CROSS=AUTO+PREDIC                                                 ! /* THE DESIRED OUTPUT
      CALL WIENER(NFPTS,AUTO,CROSS,FILT,ERROPR,1)                       ! /* COMPUTE THE  FILTER
      FILT1=FILT+NFPTS                                                  ! /* FIND THE BACK END OF THE FILTER
      FILT1=FILT1-1
      FILT2=FILT1+PREDIC
      CALL VNEG(FILT1,-1,FILT2,-1,NFPTS)
      CALL VMOV(ONE,1,FILT,1,1)                                         ! /* PUT A 1 IN THE FRONT OF THE FILTER
      IF(PREDIC.LE.1) GO TO 100
      FILT=FILT+1
      PREDIC=PREDIC-1
      CALL VCLR(FILT,1,PREDIC)                                          ! /* ZERO THE PREDICTION DISTANCE -1
  100 CONTINUE
      ITEMP=TRACE+NPTS
      ITEMP=ITEMP-1
      ITEMP2=N-1
      ITEMP1=SCRATC+NPTS+ITEMP2-1
      CALL VMOV(ITEMP,-1,ITEMP1,-1,NPTS)                                ! /* MOVE THE TRACE DOWN A FILTER LENGTH
      CALL VCLR(SCRATC,1,ITEMP2)                                        ! /* PAD THE FRONT OF THE TRACE WITH ZEROES
      CALL CONV(SCRATC,1,FILT2,-1,RESULT,1,NPTS,N)                      ! /* DO THE CONVOLUTION
      RETURN
      END
